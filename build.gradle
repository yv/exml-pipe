apply plugin: 'java'
apply plugin: 'maven-publish'

sourceSets {
    main {
        java {
            srcDirs = ['src', 'jflex_output']
        }
        resources {
            srcDirs = ['src']
            include '**/*.txt'
        }
    }
    test {
        java {
            srcDirs = ['test']
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    lex
}

task flexSrc {
    doLast {
        ant.taskdef(name: 'jflex',
                classname: 'jflex.anttask.JFlexTask',
                classpath: configurations.lex.asPath)
        ant.jflex(
                file: 'jflex_src/tokens_de.jflex',
                destdir: 'jflex_output')
    }
}

task getDeps(type: Copy) {
    from configurations.runtime
    into 'lib/'
}

compileJava.dependsOn(flexSrc)
build.finalizedBy(getDeps)

dependencies {
    lex group: 'de.jflex', name: 'jflex', version: '1.6.1'
    compile fileTree(dir: 'ext_lib', include: ['*.jar'])
    compile group: 'commons-cli', name: 'commons-cli', version: '1.2'
    compile group: 'dk.brics', name: 'automaton', version: '1.11'
    compile group: 'com.google.re2j', name: 're2j', version: '1.1'
    compile group: 'org.incava', name: 'java-diff', version: '1.1'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.4.4'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.4.4'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version:'2.4.4'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version:'2.4.4'
    compile group: 'org.apache.ant', name: 'ant', version: '1.9.4'
    compile group: 'edu.berkeley.nlp', name: 'berkeleyparser', version:'r32'
    compile group: 'nz.ac.waikato.cms.weka', name: 'weka-stable', version:'3.6.11'
    compile group: 'de.versley', name: 'exportxml', version: '1.0.8'
    compile group: 'edu.stanford.nlp', name: 'stanford-corenlp', version: '3.4.1', transitive:false
    testCompile 'junit:junit:4.12'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'de.versley'
            artifactId 'exml-pipe'
            version '2.0.1'
            from components.java
        }
    }
}
